//*~~~> SPDX-License-Identifier: MIT 

/*~~~> PHUNKS
    Thank you Phunks, your inspiration and phriendship meant the world to me and helped me through hard times.
      Never stop phighting, never surrender, always stand up for what is right and make the best of all situations towards all people.
      Phunks are phreedom phighters!
        "When the power of love overcomes the love of power the world will know peace." - Jimi Hendrix <3

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################%%%%%@@@@@((((((((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################%%%%%@@@@@((((((((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@########################################%%%%%@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@########################################%%%%%@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@###############@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@###############@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@#PHUNKYJON///////////////#PHUNKYJON//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@#PHUNKYJON///////////////#PHUNKYJON//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////@EYES////////////////////@EYES///////////////@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////@EYES////////////////////@EYES///////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////////////////////////////////////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////////////////////////////////////////////EAR@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@/////////////////////////////////////////////@@@@@@@@@@((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////NOSE@NOSE@////////////////////#####@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((SMOKE((((((((((@@@@@//////////NOSE@NOSE@////////////////////#####@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@#####//////////////////////////////##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((@@@@@#####//////////////////////////////##########@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@###################################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@###################################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((EMBER(((((,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@MOUTH&&&&&####################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((EMBER(((((,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@MOUTH&&&&&####################@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@##############################/////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((@SPLIFF@SPLIFF@SPLIFF@SPLIFF@@##############################/////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%(((((((((((((((((((((((((((((((((((@@@@@##############################//////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((@@@@@@@@@@@@@@@@@@@@@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%((((((((((((((((((((((((((((((((((((((((((((((((((((((((((((@@@@@///////////////@@@@@(((((((((((((((((((((((((%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@///////////////@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@@@@@///////////////@@@@@%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

 <~~~*/

pragma solidity >=0.8.0 <0.9.0;

import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "./interfaces/IRoleProvider.sol";
import "./interfaces/IRewardsController.sol";

interface IERC20 {
  function balanceOf(address account) external view returns (uint256);
  function allowance(address owner, address spender) external view returns (uint256);
  function transfer(address recipient, uint256 amount) external returns (bool);
  function approve(address spender, uint256 amount) external returns (bool);
  function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
  event Transfer(address indexed from, address indexed to, uint256 value);
  event Approval(address indexed owner, address indexed spender, uint256 value);
}
contract Mint is ReentrancyGuard, Pausable {
  using SafeMath for uint;
  using Counters for Counters.Counter;
  /*~~~>
    State variables
  <~~~*/
  Counters.Counter private _nftsRedeemed;
  Counters.Counter private _redemptionERC20;

  // For keeping track of marketplace NFT counts as more are created and redeemed
  uint public totalNfts;
  uint public nftsRemaining;
  uint[] public availableNfts;

  address public roleAdd;

  bytes32 public constant NFTADD = keccak256("NFT");

  bytes32 public constant REWARDS = keccak256("REWARDS");

  //*~~~> Marketplace NFT that can be used to claim rewards and act as a DAO
  struct NFT {
    uint itemId;
    uint tokenId;
    address contractAddress;
  }
  //*~~~> Tokens redeemed to create Marketplace NFTs
  struct RedemptionToken {
    uint redeemAmount;
    address contractAddress;
  }

  //*~~~> Roles for designated accessibility
  bytes32 public constant PROXY_ROLE = keccak256("PROXY_ROLE"); 
  bytes32 public constant DEV_ROLE = keccak256("DEV_ROLE");
  modifier hasAdmin(){
    require(IRoleProvider(roleAdd).hasTheRole(PROXY_ROLE, msg.sender), "DOES NOT HAVE ADMIN ROLE");
    _;
  }
  modifier hasDevAdmin(){
    require(IRoleProvider(roleAdd).hasTheRole(DEV_ROLE, msg.sender), "DOES NOT HAVE DEV ROLE");
    _;
  }

  constructor(address _role) {
    roleAdd =_role;
  }

  //*~~~> Memory mappings
  mapping (uint256 => NFT) private _idToNft;
  mapping (uint256 => RedemptionToken) private _idToRedemption;
  mapping (address => uint) private _indexToRedemptionToken;
  
  // Event declaration
  event nftClaimed(uint nftId, address creator);
  event Received(address, uint);

  /*~~~>
    Restricted access functions for mutable state variables  
  <~~~*/
  function setRoleAdd(address _role) external hasAdmin returns(bool){
    roleAdd = _role;
    return true;
  }

  /// @notice
    /*~~~>
      Function for re - setting the redemption token details
    <~~~*/
  /// @dev
    /*~~~>
      uint _redeemAmount: Amount needed to redeem a NFT; 
      address _contract: address of the redemption token;
    <~~~*/
  function resetRedemptionToken(uint _redeemAmount, address _contract) external hasAdmin returns(bool){
    uint index = _indexToRedemptionToken[_contract];
    _idToRedemption[index] = RedemptionToken(_redeemAmount, _contract);
    return true;
  }
  
  /// @notice
    /*~~~> Function for setting new redemption tokens <~~~*/
  /// @dev
    /*~~~> 
      uint _redeemAmount: amount needed to redeem for creating new NFTs;
      address _contract: address for the token;
    <~~~*/
  /// @return Bool
  function setNewRedemption(uint _redeemAmount, address _contract) external hasAdmin returns(bool){
    _redemptionERC20.increment();
    uint id = _redemptionERC20.current();
    _idToRedemption[id] = RedemptionToken(_redeemAmount, _contract);
    return true;
  }

  /// @notice
  //*~~~> For updating the total NFT array with new tokenIds
    ///@dev Can only be incremented!
    //*~~~> uint _tokenIds: token Ids to add to the array
  function setNftTokenIds(uint _amount) external hasDevAdmin returns(bool){
    for (uint i; i<_amount; i++){
      totalNfts = totalNfts+1;
      availableNfts.push(totalNfts);
    }
    nftsRemaining = availableNfts.length;
    return true;
  }

  /// @notice
    /*~~~>
      Public interaction function for redeeming new NFTs by exchanging Tokens
    <~~~*/
  /// @dev
    /*~~~>
      uint id: index to the redemption token struct;e
    <~~~*/
  /// @return Bool
  function redeemForNft(uint id) external whenNotPaused returns(bool){

    address rewardsAddress =  IRoleProvider(roleAdd).fetchAddress(REWARDS);
    address nftAddress = IRoleProvider(roleAdd).fetchAddress(NFTADD);

    /// Bring in token Interface
    RedemptionToken memory token = _idToRedemption[id];
    IERC20 tokenContract = IERC20(token.contractAddress);
    /// Check allowance
    uint256 allowance = tokenContract.allowance(msg.sender, address(this));
    require(allowance >= token.redeemAmount, "Check the token allowance");
    /// Execute transfer
    tokenContract.transferFrom(msg.sender, rewardsAddress, token.redeemAmount);
    
    ///Keeping track of total claimed
    _nftsRedeemed.increment();
    uint256 nftId = _nftsRedeemed.current();
    /// Using the new ID as a nonce to generate a random tokenId from the available NFTs
    uint tokenId = getQuasiRandom(nftId);
    IERC721(nftAddress).safeTransferFrom(address(this), msg.sender, tokenId);
    _idToNft[nftId] = NFT(nftId, tokenId, nftAddress);

    bool depositSuccess = IRewardsController(rewardsAddress).depositERC20Rewards(token.redeemAmount, token.contractAddress);
    require(depositSuccess);

    bool hodlerSuccess = IRewardsController(rewardsAddress).createNftHodler(nftId);
    require(hodlerSuccess);

    emit nftClaimed(nftId, msg.sender);
    return true;
  }

  /// @notice
  /*~~~>
    Internal fuction for fetching a random tokenId from unclaimed NFTs
  <~~~*/
    function getQuasiRandom(uint nonce) internal returns(uint ramndomNumber){
      uint quasiRandom = uint(keccak256(abi.encodePacked(block.timestamp, msg.sender, nonce))) % nftsRemaining;
      require(quasiRandom <= nftsRemaining);
      uint newId = availableNfts[quasiRandom];
      /// Shifting the last ID to the selected id position
      availableNfts[quasiRandom] = availableNfts[nftsRemaining-1];
      /// Popping off last ID
      availableNfts.pop();
      nftsRemaining = availableNfts.length;
      return newId;
    }

  /// @notice
  /*~~~>
  Functions for retrieving memory items
  <~~~*/
  function fetchRedemptionTokens() external view returns (RedemptionToken[] memory) {
    uint itemCount = _redemptionERC20.current();
    RedemptionToken[] memory tokens = new RedemptionToken[](itemCount);
    uint currentIndex;
    for (uint i; i < itemCount; i++) {
      RedemptionToken storage currentItem = _idToRedemption[i + 1];
      tokens[currentIndex] = currentItem;
      currentIndex++;
    }
    return tokens;
  }

  function fetchNFTsCreated() external view returns (NFT[] memory) {
    uint itemCount = _nftsRedeemed.current();
    NFT[] memory nfts = new NFT[](itemCount);
    uint currentIndex;
    for (uint i; i < itemCount; i++) {
      NFT storage currentItem = _idToNft[i + 1];
      nfts[currentIndex] = currentItem;
      currentIndex++;
    }
    return nfts;
  }

  function fetchNFTsCreatedCount() external view returns (uint) {
    uint itemCount = _nftsRedeemed.current();
    return itemCount;
  }

  ///@notice DEV operations for emergency functions
  function pause() external hasDevAdmin {
      _pause();
  }
  function unpause() external hasDevAdmin {
      _unpause();
  }

  //*~~~> Fallback functions
  ///@notice
  /*~~~> External ETH transfer forwarded to role provider contract <~~~*/
  event FundsForwarded(uint value, address _from, address _to);
  receive() external payable {
    payable(roleAdd).transfer(msg.value);
      emit FundsForwarded(msg.value, msg.sender, roleAdd);
  }
  function onERC1155Received(address, address, uint256, uint256, bytes memory) external virtual returns (bytes4) {
        return this.onERC1155Received.selector;
    }
  function onERC721Received(
      address, 
      address, 
      uint256, 
      bytes calldata
    )external pure returns(bytes4) {
        return bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"));
  }
}
